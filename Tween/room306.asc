// Overlay tests (room 306) script file

Overlay* _overlay;

#ifdef SCRIPT_API_v360
int _overlayWidth;
int _overlayHeight;
#endif

function _RestoreOverlay() {
  _overlay.X = 0;
  _overlay.Y = 0;

  #ifdef SCRIPT_API_v360
  _overlay.Width = _overlayWidth;
  _overlay.Height = _overlayHeight;
  _overlay.Transparency = 0;
  #endif
}
  
function testPosition() {
  Test("Position");
  
  _overlay.TweenPosition(0.5, Room.Width, Room.Height);
  ExpectIntToBe(_overlay.X, Room.Width, "Position X");
  ExpectIntToBe(_overlay.Y, Room.Height, "Position Y");
  
  _RestoreOverlay();
  
  _overlay.TweenX(0.5, Room.Width);
  ExpectIntToBe(_overlay.X, Room.Width, "X X");
  ExpectIntToBe(_overlay.Y, 0, "X Y");
  
  _RestoreOverlay();
  
  _overlay.TweenY(0.5, Room.Height);
  ExpectIntToBe(_overlay.X, 0, "Y X");
  ExpectIntToBe(_overlay.Y, Room.Height, "Y Y");
  
  _RestoreOverlay();
  
  _overlay.TweenPosition(0.5, Room.Width, Room.Height, eEaseLinearTween, eNoBlockTween);
  _overlay.StopTweenPosition(eFinishTween);
  ExpectIntToBe(_overlay.X, Room.Width, "Stop X");
  ExpectIntToBe(_overlay.Y, Room.Height, "Stop Y");
  
  _RestoreOverlay();
}

function testSize() {
  #ifdef SCRIPT_API_v360
  Test("Size");
  
  _overlay.TweenSize(0.5, Room.Width, Room.Height);
  ExpectIntToBe(_overlay.Width, Room.Width, "Size Width");
  ExpectIntToBe(_overlay.Height, Room.Height, "Size Height");
  
  _RestoreOverlay();
  
  _overlay.TweenWidth(0.5, Room.Width);
  ExpectIntToBe(_overlay.Width, Room.Width, "Width Width");
  ExpectIntToBe(_overlay.Height, _overlayHeight, "Width Height");
  
  _RestoreOverlay();
  
  _overlay.TweenHeight(0.5, Room.Height);
  ExpectIntToBe(_overlay.Width, _overlayWidth, "Height Width");
  ExpectIntToBe(_overlay.Height, Room.Height, "Height Height");
  
  _RestoreOverlay();
  
  _overlay.TweenSize(0.5, Room.Width, Room.Height, eEaseLinearTween, eNoBlockTween);
  _overlay.StopTweenSize(eFinishTween);
  ExpectIntToBe(_overlay.Width, Room.Width, "Stop Width");
  ExpectIntToBe(_overlay.Height, Room.Height, "Stop Height");
  
  _RestoreOverlay();
  #endif
}

function testTransparency() {
  #ifdef SCRIPT_API_v360
  Test("Transparency");

  _overlay.Transparency = 0;
  _overlay.TweenTransparency(0.5, 50);
  ExpectIntToBe(_overlay.Transparency, 50, "Transparency");

  _RestoreOverlay();

  _overlay.Transparency = 100;
  _overlay.TweenTransparency(0.5, -20);
  ExpectIntToBe(_overlay.Transparency, 0, "Min Transparency");

  _RestoreOverlay();

  _overlay.TweenTransparency(0.5, 120);
  ExpectIntToBe(_overlay.Transparency, 100, "Max Transparency");

  _RestoreOverlay();

  _overlay.TweenTransparency(0.5, 50, eEaseLinearTween, eNoBlockTween);
  _overlay.StopTweenTransparency(eFinishTween);
  ExpectIntToBe(_overlay.Transparency, 50, "Stop Transparency");

  _RestoreOverlay();

  _overlay.TweenFadeOut(0.5);
  ExpectIntToBe(_overlay.Transparency, 100, "FadeOut");

  _overlay.TweenFadeIn(0.5);
  ExpectIntToBe(_overlay.Transparency, 0, "FadeIn");

  _RestoreOverlay();
  #endif
}


function testStopAll() {
  Test("StopAll");
 
  _overlay.TweenPosition(0.5, Room.Width, Room.Height, eEaseLinearTween, eNoBlockTween);
  #ifdef SCRIPT_API_v360
  _overlay.TweenTransparency(0.5, 50, eEaseLinearTween, eNoBlockTween);
  #endif
  
  _overlay.StopAllTweens(eFinishTween);

  ExpectIntToBe(_overlay.X, Room.Width, "X");
  ExpectIntToBe(_overlay.Y, Room.Height, "Y");
  #ifdef SCRIPT_API_v360
  ExpectIntToBe(_overlay.Transparency, 50, "Transparency");
  #endif
  
  _RestoreOverlay();
}

function room_Load()
{
  _overlay = Overlay.CreateRoomTextual(0, 0, 120, eFontNormal, 15, "Hello, world!");
  #ifdef SCRIPT_API_v360
  _overlayWidth = _overlay.Width;
  _overlayHeight = _overlay.Height;
  #endif
}

function room_AfterFadeIn()
{
  StartCutscene(eSkipESCOnly);
  
  testPosition();
  testSize();
  testTransparency();
  testStopAll();
  
  EndCutscene();
  player.ChangeRoom(99);
}

function room_Leave()
{
 _overlay.Remove();
 _overlay = null;
}
